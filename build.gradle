import org.w3c.dom.Element

String property(String name) {
    return project.getProperty(name)
}

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.+', changing: true
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

repositories {
    maven {
        name 'LDTTeam - Modding'
        url 'https://ldtteam.jfrog.io/ldtteam/modding/'
    }
    maven {
        url "https://maven.tehnut.info"
    }
    maven {
        url "https://www.cursemaven.com"
    }
    maven {
        url "https://harleyoconnor.com/maven"
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'

String modName = property("modName")
String modVersion = property("modVersion")
String mcVersion = property("mcVersion")

version = "${mcVersion}-${modVersion}"
group = property("group")
archivesBaseName = modName

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: 'official', version: mcVersion

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            if (project.hasProperty('mcUuid')) {
                args '--uuid', property('mcUuid')
            }
            if (project.hasProperty('mcUsername')) {
                args '--username', property('mcUsername')
            }
            if (project.hasProperty('mcAccessToken')) {
                args '--accessToken', property('mcAccessToken')
            }

            mods {
                dynamictrees {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            mods {
                dynamictrees {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            args '--mod', 'dynamictrees', '--all', '--output', file('src/generated/resources/')

            mods {
                dynamictrees {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

dependencies {
    // Not sure if we need this one, what is a "forge" anyway?
    minecraft "net.minecraftforge:forge:${mcVersion}-${property("forgeVersion")}"

    // Compile Hwyla API, but don't include in runtime.
    compileOnly fg.deobf("mcp.mobius.waila:Hwyla:${property("hwylaVersion")}:api")
    // At runtime, use the full Hwyla mod.
    runtimeOnly fg.deobf("mcp.mobius.waila:Hwyla:${property("hwylaVersion")}")

    // Compile JEI API, but don't include in runtime.
    compileOnly fg.deobf("mezz.jei:jei-${mcVersion}:${property("jeiVersion")}:api")
    // At runtime, use the full JEI mod.
    runtimeOnly fg.deobf("mezz.jei:jei-${mcVersion}:${property("jeiVersion")}")

    // At runtime, use Patchouli mod (for the guide book, which is Json so we don't need the API).
    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:${property("patchouliVersion")}")

    //At runtime use ComputerCraft for creating the growth chambers
    runtimeOnly fg.deobf("curse.maven:cc-tweaked-282001:3236650")

    // Compile full Serene Seasons mod and use in runtime.
    implementation fg.deobf("curse.maven:SereneSeasons-291874:3202233")

    // At runtime, use suggestion provider fix mod.
    runtimeOnly fg.deobf("com.harleyoconnor.suggestionproviderfix:SuggestionProviderFix:${mcVersion}-${property("suggestionProviderFixVersion")}")
}

jar {
    manifest {
        attributes([
                "Specification-Title": project.name,
                "Specification-Vendor": "ferreusveritas",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor" :"ferreusveritas",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allJava
}

jar.finalizedBy('reobfJar')

java {
    withSourcesJar()
}

curseforge {
    if (project.hasProperty("curseApiKey") && project.hasProperty("curseFileType")) {
        apiKey = property("curseApiKey")

        project {
            id = '252818'

            addGameVersion '1.16.4'
            addGameVersion mcVersion

            changelog = file('build/changelog.md')
            changelogType = 'markdown'
            releaseType = property("curseFileType")

            addArtifact sourcesJar

            mainArtifact(jar) {
                relations {
                    optionalDependency 'dynamictreesplus'
                    optionalDependency 'chunk-saving-fix'
                }
            }
        }
    } else {
        project.logger.log(LogLevel.WARN, "API Key and file type for CurseForge not detected; uploading will be disabled.")
    }
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            setArtifactId("${modName}-${mcVersion}")
            setVersion(modVersion)

            from components.java

            pom {
                url = "https://github.com/ferreusveritas/${modName}"
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://mit-license.org'
                    }
                }
                developers {
                    developer {
                        id = "ferreusveritas"
                        name = "Ferreus Veritas"
                    }
                    developer {
                        id = "supermassimo"
                        name = "Max Hyper"
                    }
                    developer {
                        id = "Harleyoc1"
                        name = "Harley O'Connor"
                        email = "Harleyoc1@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/ferreusveritas/${modName}.git"
                    developerConnection = "scm:git:ssh://github.com/ferreusveritas/${modName}.git"
                    url = "https://github.com/ferreusveritas/${modName}"
                }
            }

            pom.withXml { xmlProvider ->
                final Element element = xmlProvider.asElement();

                // Clear dependencies.
                element.getChildNodes().each { node ->
                    if (node.getNodeName() == "dependencies")
                        element.removeChild(node)
                }
            }
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
        if (project.hasProperty("harleyOConnorMavenUsername") && project.hasProperty("harleyOConnorMavenPassword")) {
            maven {
                name = "HarleyOConnorMaven"
                url = uri("https://harleyoconnor.com/maven")
                credentials {
                    username = property("harleyOConnorMavenUsername")
                    password = property("harleyOConnorMavenPassword")
                }
            }
        } else {
            project.logger.log(LogLevel.WARN, "Credentials for maven not detected; it will be disabled.")
        }
    }
}

idea {
    module {
        downloadSources = true
    }
}